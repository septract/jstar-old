(********************************************************
   This file is part of jStar
        src/utils/system.ml
   Release
        $Release$
   Version
        $Rev$
   $Copyright$

   jStar is distributed under a BSD license,  see,
      LICENSE.txt
 ********************************************************)

open Debug
open Jstar_std
open Printf     (* DBG *)

let java_path_delimiter = if Sys.os_type = "Windows" then ";" else ":"
let java_path_delimiter_re = Str.regexp (java_path_delimiter ^ "+")

let getenv variable = 
  try Sys.getenv variable with Not_found -> ""

let getenv_dirlist variable = 
  Str.split java_path_delimiter_re (getenv variable)

   
(* read a file into a string *)
let string_of_file fname =
  let ichan = if fname = "-" then stdin else open_in fname
  and str = String.create 1024
  and buf = Buffer.create 1024 in
  let rec loop () =
    let len = input ichan str 0 1024 in
    Buffer.add_substring buf str 0 len;
    if len = 0 then Buffer.contents buf else loop () in
  let s = loop () in
  close_in ichan;
  s



let parse_file pars lexe fname ftype = 
  try 
    if log log_phase then 
      Printf.printf "Start parsing %s in %s...\n" ftype fname;
    let ichan = open_in fname in 
    let ret = pars lexe (Lexing.from_channel ichan) in 
    Parsing.clear_parser ();
    close_in ichan;
    if log log_phase then Printf.printf "Parsed %s!\n" fname;
    ret
  with Parsing.Parse_error -> Printf.printf "Failed to parse %s\n" fname; exit 1
  |  Failure s ->  Printf.printf "Failed to parse %s\n%s\n" fname s; exit 1 

(* 
  Check if file exists in current directory, or in list of directories supplied.  
  Returns full filename if found,
  If not raises Not_found 
*)
let find_file_from_dirs dirs fname =
  if Sys.file_exists fname then fname 
  else 
    let f x = Filename.concat x fname in 
    f (List.find (function d -> Sys.file_exists (f d)) dirs)

let rec fs_postorder m f =
  if Sys.file_exists f then begin
    if Sys.is_directory f then begin
      let children = Array.map (Filename.concat f) (Sys.readdir f) in
      Array.iter (fs_postorder m) children
    end;
    m f
  end

let fs_filter p f =
  let r = ref [] in
  fs_postorder (fun x -> if p x then r =:: x) f; !r

let rm_rf f = 
  fs_postorder 
    (fun x -> if Sys.is_directory x then Unix.rmdir x else Unix.unlink x) f

let rec mkdir_p dir =
  if Sys.file_exists dir then begin
    if not (Sys.is_directory dir) then 
      raise (Unix.Unix_error (Unix.EEXIST, "mkdir_p", dir))
  end else begin
    mkdir_p (Filename.dirname dir);
    Unix.mkdir dir 0o755
  end

let is_executable_available fn =
  try
    let candidate = find_file_from_dirs (getenv_dirlist "PATH") fn in
    Unix.access candidate [Unix.X_OK; Unix.R_OK];
    true 
  with _ -> false

let is_file ext fn =
  Sys.file_exists fn && not (Sys.is_directory fn) &&
  StringH.ends_with (String.lowercase ext) (String.lowercase fn)

(* TODO(rgrig): Thee should probably depend on the terminal. *)
(* TODO(rgrig): Is there a (nice) ncurses ocaml interface? *)
let terminal_red = "\x1B[1;31m"
let terminal_green = "\x1B[1;32m"
let terminal_white = "\x1B[0m"

